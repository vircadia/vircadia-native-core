<@include gpu/Config.slh@>
<$VERSION_HEADER$>
//  Generated on <$_SCRIBE_DATE$>
//
//  simple_transparent.frag
//  fragment shader
//
//  Created by Andrzej Kapolka on 9/15/14.
//  Copyright 2014 High Fidelity, Inc.
//
//  Distributed under the Apache License, Version 2.0.
//  See the accompanying file LICENSE or http://www.apache.org/licenses/LICENSE-2.0.html
//
<@include DefaultMaterials.slh@>

<@include DeferredGlobalLight.slh@>
<$declareEvalGlobalLightingAlphaBlendedWithHaze()$>

<@include gpu/Transform.slh@>
<$declareStandardCameraTransform()$>

<@include render-utils/ShaderConstants.h@>

// the interpolated normal
layout(location=RENDER_UTILS_ATTR_NORMAL_WS) in vec3 _normalWS;
layout(location=RENDER_UTILS_ATTR_NORMAL_MS) in vec3 _normalMS;
layout(location=RENDER_UTILS_ATTR_COLOR) in vec4 _color;
layout(location=RENDER_UTILS_ATTR_TEXCOORD01) in vec4 _texCoord01;
#define _texCoord0 _texCoord01.xy
#define _texCoord1 _texCoord01.zw
layout(location=RENDER_UTILS_ATTR_POSITION_MS) in vec4 _positionMS;
layout(location=RENDER_UTILS_ATTR_POSITION_ES) in vec4 _positionES;

layout(location=0) out vec4 _fragColor0;

// For retro-compatibility
#define _normal      _normalWS
#define _modelNormal _normalMS
#define _position    _positionMS
#define _eyePosition _positionES

<@include procedural/ProceduralCommon.slh@>

#line 1001

//PROCEDURAL_BLOCK_BEGIN

vec3 getProceduralColor() {
    return _color.rgb;
}

float getProceduralColors(inout vec3 diffuse, inout vec3 specular, inout float shininess) {
    return 1.0;
}

float getProceduralFragment(inout ProceduralFragment proceduralData) {
    return 1.0;
}

float getProceduralFragmentWithPosition(inout ProceduralFragmentWithPosition proceduralData) {
    return 1.0;
}

//PROCEDURAL_BLOCK_END

#line 2030
void main(void) {
    vec3 normal = normalize(_normalWS.xyz) * (2.0 * float(gl_FrontFacing) - 1.0);
    vec3 diffuse = _color.rgb;
    float alpha = _color.a;
    float occlusion = DEFAULT_OCCLUSION;
    vec3 fresnel = DEFAULT_FRESNEL;
    float metallic = DEFAULT_METALLIC;
    vec3 emissive = DEFAULT_EMISSIVE;
    float roughness = DEFAULT_ROUGHNESS;

    float emissiveAmount = 0.0;

    TransformCamera cam = getTransformCamera();
    vec3 posEye = _positionES.xyz;

#ifdef PROCEDURAL_V1
    diffuse = getProceduralColor().rgb;
    emissiveAmount = 1.0;
    emissive = vec3(1.0);
#elif defined(PROCEDURAL_V2)
    vec3 specular = DEFAULT_SPECULAR;
    float shininess = DEFAULT_SHININESS;
    emissiveAmount = getProceduralColors(diffuse, specular, shininess);
    roughness = max(0.0, 1.0 - shininess / 128.0);
    metallic = length(specular);
    emissive = vec3(clamp(emissiveAmount, 0.0, 1.0));
#elif defined(PROCEDURAL_V3) || defined(PROCEDURAL_V4)
#if defined(PROCEDURAL_V3)
    ProceduralFragment proceduralData = ProceduralFragment(
#else
    vec4 position = cam._viewInverse * _positionES;
    ProceduralFragmentWithPosition proceduralData = ProceduralFragmentWithPosition(
        position.xyz,
#endif
        normal,
        diffuse,
        fresnel,
        emissive,
        alpha,
        roughness,
        metallic,
        occlusion,
        DEFAULT_SCATTERING
    );

#if defined(PROCEDURAL_V3)
    emissiveAmount = getProceduralFragment(proceduralData);
#else
    emissiveAmount = getProceduralFragmentWithPosition(proceduralData);
#endif
    occlusion = proceduralData.occlusion;
    normal = proceduralData.normal;
    diffuse = proceduralData.diffuse;
    fresnel = proceduralData.specular;
    metallic = proceduralData.metallic;
    emissive = proceduralData.emissive;
    roughness = proceduralData.roughness;
    alpha = proceduralData.alpha;

#if defined(PROCEDURAL_V4)
    position = vec4(proceduralData.position, 1.0);
    vec4 posEye4 = cam._view * position;
    posEye = posEye4.xyz;
    vec4 posClip = cam._projection * posEye4;
    gl_FragDepth = 0.5 * (posClip.z / posClip.w + 1.0);
#endif

#endif

    if (emissiveAmount > 0.0) {
        _fragColor0 = vec4(diffuse, alpha);
    } else {
        _fragColor0 = vec4(evalGlobalLightingAlphaBlendedWithHaze(
            cam._viewInverse,
            1.0,
            occlusion,
            posEye,
            normal,
            diffuse,
            fresnel,
            metallic,
            emissive,
            roughness, alpha),
            alpha);
    }
}
