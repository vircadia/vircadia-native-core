<@include gpu/Config.slh@>
<$VERSION_HEADER$>
//  Generated on <$_SCRIBE_DATE$>
//  sdf_text3D.frag
//  fragment shader
//
//  Created by Bradley Austin Davis on 2015-02-04
//  Based on fragment shader code from 
//  https://github.com/paulhoux/Cinder-Samples/blob/master/TextRendering/include/text/Text.cpp 
//  Distributed under the Apache License, Version 2.0.
//  See the accompanying file LICENSE or http://www.apache.org/licenses/LICENSE-2.0.html

<@include DeferredBufferWrite.slh@>
<@include render-utils/ShaderConstants.h@>

layout(binding=0) uniform sampler2D Font;
layout(location=RENDER_UTILS_UNIFORM_TEXT_OUTLINE) uniform bool Outline;
layout(location=RENDER_UTILS_UNIFORM_TEXT_COLOR) uniform vec4 Color;

// the interpolated normal
layout(location=RENDER_UTILS_ATTR_NORMAL_WS) in vec3 _normalWS;
layout(location=RENDER_UTILS_ATTR_TEXCOORD01) in vec4 _texCoord01;
#define _texCoord0 _texCoord01.xy
#define _texCoord1 _texCoord01.zw

#define TAA_TEXTURE_LOD_BIAS    -3.0

const float interiorCutoff = 0.8;
const float outlineExpansion = 0.2;
const float taaBias = pow(2.0, TAA_TEXTURE_LOD_BIAS);

float evalSDF(vec2 texCoord) {
    // retrieve signed distance
    float sdf = textureLod(Font, texCoord, TAA_TEXTURE_LOD_BIAS).g;
    if (Outline) {
        if (sdf > interiorCutoff) {
            sdf = 1.0 - sdf;
        } else {
            sdf += outlineExpansion;
        }
    }
    // Rely on TAA for anti-aliasing
    return step(0.5, sdf);
}

void main() {
    vec2 dxTexCoord = dFdx(_texCoord0) * 0.5 * taaBias;
    vec2 dyTexCoord = dFdy(_texCoord0) * 0.5 * taaBias;

	// Perform 4x supersampling for anisotropic filtering
    float a;
	a = evalSDF(_texCoord0);
	a += evalSDF(_texCoord0+dxTexCoord);
	a += evalSDF(_texCoord0+dyTexCoord);
	a += evalSDF(_texCoord0+dxTexCoord+dyTexCoord);
    a *= 0.25;

    // discard if invisible
    if (a < 0.01) {
        discard;
    }

    packDeferredFragment(
        normalize(_normalWS),
        a * Color.a,
        Color.rgb,
        DEFAULT_ROUGHNESS,
        DEFAULT_METALLIC,
        DEFAULT_EMISSIVE,
        DEFAULT_OCCLUSION,
        DEFAULT_SCATTERING);
}